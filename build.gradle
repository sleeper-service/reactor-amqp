/*
 * Copyright (c) 2011-2013 GoPivotal, Inc. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *       http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
description = 'Reactor AMQP components'

ext {
	gradleVersion = '2.1'
	gradleScriptDir = "${rootProject.projectDir}/gradle"

	// Logging
	slf4jVersion = '1.7.7'
	logbackVersion = '1.1.2'

	// Languages
	groovyVersion = '2.3.6'

	// Reactor
	reactorVersion = '2.0.0.BUILD-SNAPSHOT'

	// AMQP
	amqpVersion = '3.3.5'

}

buildscript {
	repositories {
		maven { url "http://repo.spring.io/plugins-release" }
	}
	dependencies {
		classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.6'
	}
}

apply from: "$gradleScriptDir/setup.gradle"

allprojects {
	apply plugin: 'propdeps'
	apply plugin: 'java'
	apply plugin: 'idea'

	configurations.all {
		exclude group: "commons-logging"
	}

	[compileJava, compileTestJava]*.options*.compilerArgs = [
			"-Xlint:serial",
			"-Xlint:varargs",
			"-Xlint:cast",
			"-Xlint:classfile",
			"-Xlint:dep-ann",
			"-Xlint:divzero",
			"-Xlint:empty",
			"-Xlint:finally",
			"-Xlint:overrides",
			"-Xlint:path",
			"-Xlint:processing",
			"-Xlint:static",
			"-Xlint:try",
			"-Xlint:-options", // intentionally disabled
			"-Xlint:-fallthrough", // intentionally disabled
			"-Xlint:-rawtypes", // TODO enable and fix warnings
			"-Xlint:-deprecation", // TODO enable and fix warnings
			"-Xlint:-unchecked" // TODO enable and fix warnings
	]

	compileJava {
		sourceCompatibility = 1.7
		targetCompatibility = 1.7
	}

	compileTestJava {
		sourceCompatibility = 1.8
		targetCompatibility = 1.8
	}

	test {
		systemProperty("java.awt.headless", "true")
		systemProperty("testGroups", project.properties.get("testGroups"))
		scanForTestClasses = false
		include '**/*Tests.*'
		exclude '**/*Abstract*.*'
	}

	group = 'org.projectreactor.amqp'
	repositories {
		mavenLocal()
		maven { url 'http://repo.spring.io/libs-snapshot' }
		mavenCentral()
		jcenter()
	}

	dependencies {
		// Logging
		compile "org.slf4j:slf4j-api:$slf4jVersion"

		// Testing
		testCompile "junit:junit-dep:4.11",
				"org.hamcrest:hamcrest-library:1.3",
				"org.codehaus.groovy:groovy-all:$groovyVersion"

		testRuntime "ch.qos.logback:logback-classic:$logbackVersion",
				"org.slf4j:jcl-over-slf4j:$slf4jVersion"
	}
}

project('amqp-stream') {
	description = 'Reactor Stream from/to AMQP binding'

	dependencies {
		// Reactor
		compile "org.projectreactor:reactor-core:$reactorVersion"

		//RabbitMQ
		compile "com.rabbitmq:amqp-client:$amqpVersion"
	}
}